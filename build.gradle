plugins {
    id "com.github.kt3k.coveralls" version "2.6.3"
}

// constants declaration
ext {
    jdkVersion = 1.8

    jettyVersion = "9.3.12.v20160915"
    junitVersion = "4.12"
    jacksonVersion = "2.5.3"
    log4jVersion = "2.7"
    jetbrainsAnnotationVersion = "15.0"
    okhttpVersion = "3.6.0"
    jerseyVersion = "2.25.1"
    gsonjVersion = "2.7"
    postgresVersion = "9.4-1200-jdbc41"
    jetbrainsAnnotationVersion = "15.0"
    hibernateVersion = "5.2.3.Final"
    websocketVersion = "9.4.3.v20170317"
    jolVersion = "0.8"
}

allprojects {
    group = "technoatom"
    version = "1.0-SNAPSHOT"

    apply plugin: "java"
    apply plugin: 'checkstyle'
    apply plugin: "jacoco"

    repositories {
        mavenCentral()
    }

    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
}

subprojects {
    checkstyle {
        ignoreFailures = false
        toolVersion = '7.5'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.destination "$rootProject.buildDir/report/${project.name}.html"
            html.stylesheet resources.text.fromFile(rootProject.file('config/checkstyle/checkstyle-custom.xsl'))
        }
    }

}

// common libraries declaration
ext.libraries = [
        jetty_server : "org.eclipse.jetty:jetty-server:$jettyVersion",
        jetty_servlet: "org.eclipse.jetty:jetty-servlet:$jettyVersion",
        junit: "junit:junit:$junitVersion",
        log4j: [
            "org.apache.logging.log4j:log4j-api:$log4jVersion",
            "org.apache.logging.log4j:log4j-core:$log4jVersion"
        ],
        jetbrainsAnnotations: "org.jetbrains:annotations:$jetbrainsAnnotationVersion",
        okhttp: "com.squareup.okhttp3:okhttp:$okhttpVersion",
        jersey_server: "org.glassfish.jersey.core:jersey-server:$jerseyVersion",
        jersey_containers: "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion",
        jersey_test:
                "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:$jerseyVersion",
        gson: "com.google.code.gson:gson:$gsonjVersion",
        postgres: "org.postgresql:postgresql:$postgresVersion",
        hibernate: "org.hibernate:hibernate-core:$hibernateVersion",
        websocketclient: "org.eclipse.jetty.websocket:websocket-client:$websocketVersion",
        websocketserver: "org.eclipse.jetty.websocket:websocket-server:$websocketVersion",
        websocketapi: "org.eclipse.jetty.websocket:websocket-api:$websocketVersion",
        jackson: "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
        jol: "org.openjdk.jol:jol-core:$jolVersion",
        jol_samples: "org.openjdk.jol:jol-samples:$jolVersion"
]

// code coverage settings
jacocoTestReport {
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    onlyIf = {
        true
    }

    reports {
            xml.enabled = true
            html.enabled = true
    }
    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}

coveralls {
    sourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs).files.absolutePath
}
